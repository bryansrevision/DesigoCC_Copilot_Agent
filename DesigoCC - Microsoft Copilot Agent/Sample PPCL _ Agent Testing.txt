00010     C     PROJECT: KEDRION BIOPHARMA FACILITIES UPGRADE
00020     C     PROJECT #: 44OP-212912
00030     C     CONTROLLER:	MELVILLENODE01
00040     C     PROGRAM: MELVILLE.AHU-104.PPCL
00050     C
00060     C
00070     C --------------------------------------------------------------------------------
00080     C REV	DATE	PROGRAMMER	COMMENTS
00090     C --------------------------------------------------------------------------------
00100     C  A	10/2/17	JOSHUA MORGAN	INITIAL PROGRAMMING
00110     C --------------------------------------------------------------------------------
00120     C
00200     ONPWRT(300)
00210     C
00220     C	*******MODBUS INTERGRATION SETTINGS*******
00230     C
00240     SAMPLE(600) "%X%KATHABAR:IPADDR1" = 10
00250     SAMPLE(600) "%X%KATHABAR:IPADDR2" = 45
00260     SAMPLE(600) "%X%KATHABAR:IPADDR3" = 106
00270     SAMPLE(600) "%X%KATHABAR:IPADDR4" = 132
00280     SAMPLE(600) "%X%KATHABAR:IPPORT" = 502
00290     C
00300     DEFINE(X,"AHU104.")
00310     C
00320     C
00330     "AHU104.KATHABAR.KATHENE.LVL" = "AHU104.KATHABAR:KATHENE LVL" * 0.1
00340     C
00350     "%X%PHT.SP" = 50
00360     C
00370     C LOOP(128,"%X%PHT","%X%PHT.LPO","%X%PHT.SP",1000,10,10,2,50,0,100,0)
00380     ADAPTS("%X%PHT","%X%PHT.LPO","%X%PHT.SP",4,3,15,1,32,160,0,100,0.25,0,"%X%PHT.LPO.ERR")
00390     C
00400     C	******RAMP TIMER FOR THE OUTDOOR AIR DAMPER*******
00410     C
00420     SECNDS = SECNDS * ("%X%LTD" .NE. ON)
00430     C
00440     SECND1 = SECND1 * ("%X%OCC" .EQ. ON)
00450     C
00460     TABLE(SECNDS,"%X%OAD.RAMP",0,0,300,100)
00470     C
00480     TABLE(SECND1,"%X%OCC.RAMP",0,0,300,100)
00490     C
04500     IF("%X%LTD" .EQ. ON) THEN GOTO 10000
04510     C
04640     IF("%X%OCC" .EQ. OFF) THEN GOTO 25000
04650     C
04660     IF("%X%OCC" .EQ. ON) THEN GOTO 5000
04670     C
04680     C
04690     C	*******TURN ON EXHAUST FAN******
04700     C
05000     ON("%X%SAF.SS")
05010     C
05020     C	*******TURN ON FAN PROOF POINTS FOR HARRIS IS CONDITIONER FAN IS ON********
05030     C
05040     IF("%X%KATHABAR:COND RUN" .EQ. ON) THEN ON("CR111.FANPF","CR113.FANPF","EF04.01.SS") ELSE GOTO 5100
05050     C
05060     MIN("%X%OAD",100,"%X%OAD.RAMP","%X%OCC.RAMP")
05070     C
05080     GOTO 5160
05090     C
05100     IF("%X%KATHABAR:COND RUN" .NE. ON) THEN OFF("CR111.FANPF","CR113.FANPF","EF04.01.SS")
05110     C
05120     GOTO 25100
05130     C
05140     C	*******SUPPLY AIR TEMPERATURE CONTROL LOOP THROUGH COOLING COIL TEMP SENSOR*****
05150     C
05160     LOOP(0,"%X%CCT","%X%SAT.LPO","%X%SAT.SP",5000,10,0,2,50,0,100,0)
05170     C
06000     C	*******SAT.LPO CONTROLS HEATING AND COOLING VALVES*******
06010     C
06020     TABLE("%X%SAT.LPO","%X%PHV.VRT",0,100,49,0)
06030     C
06040     TABLE("%X%SAT.LPO","%X%CCV",51,0,100,100)
06050     C
06060     MAX("%X%PHV","%X%PHV.VRT","%X%PHT.LPO")
06070     C
06080     GOTO 32767
06090     C
06100     C
06110     C	*******LOW TEMPERATURE PROTECTION******
06120     C
10000     RELEAS("%X%CCV","%X%PHV","%X%OAD","%X%SAF.SS")
10010     C
10020     OFF("%X%SAF.SS")
10030     C
10040     "%X%CCV" = 100
10050     C
10060     "%X%PHV" = 100
10070     C
10080     "%X%OAD" = 0
10090     C
10100     "%X%SAT.LPO" = 50
10110     C
10120     GOTO 32767
10130     C
10140     C
10150     C	*******SYSTEM OFF******
10160     C
25000     C RELEAS(@OPER,"%X%CCV","%X%PHV","%X%OAD","EF04.01.SS","%X%SAF.SS")
25010     C
25020     "%X%SAT.LPO" = 50
25030     C
25040     "%X%CCV" = 0
25050     C
25060     "%X%OAD" = 0
25070     C
25080     OFF("EF04.01.SS","%X%SAF.SS")
25090     C
25100     SET("%X%PHV","%X%PHT.LPO")
25110     C
32767     GOTO 200